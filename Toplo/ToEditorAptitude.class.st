Class {
	#name : #ToEditorAptitude,
	#superclass : #BrAptitude,
	#traits : 'TBlTextStyleable',
	#classTraits : 'TBlTextStyleable classTrait',
	#instVars : [
		'attributesBuilder',
		'text',
		'styler'
	],
	#category : #'Toplo-Editor-Look'
}

{ #category : #accessing }
ToEditorAptitude >> attributesBuilder [
	<return: #BlTextAttributesBuilder>
	
	^ attributesBuilder
]

{ #category : #initialization }
ToEditorAptitude >> initialize [
	super initialize.
	
	styler := BlTextNullStyler new.
	text := '' asRopedText.
	
	attributesBuilder := BlTextDelegatedAttributesBuilder new attributesTarget: text.
	attributesBuilder beNotOverwritableByStyler.
	attributesBuilder whenAdded: [  ].
	
	self add: BrLayoutResizerAptitude inherit
]

{ #category : #initialization }
ToEditorAptitude >> initializeListeners [
	super initializeListeners.
	
	self when: BrEditorStylerChanged do: [ :anEvent | self styler: anEvent styler ].
	self when: BrEditorTextChanged do: [ :anEvent | self text: anEvent text ].
	self when: BrEditorModeChanged do: [ :anEvent | self mode: anEvent mode ]
]

{ #category : #initialization }
ToEditorAptitude >> initializeRequests [
	super initializeRequests.

	self request: BrEditorStylerRequest new.
	self request: BrEditorTextRequest new.
	self request: BrEditorModeRequest new
]

{ #category : #accessing }
ToEditorAptitude >> mode: aTextEditorMode [	
	self widgetDo: [ :aWidget |
		aWidget editor editorMode: aTextEditorMode.
		aWidget states addState: (ToEditorModeState mode: aTextEditorMode class) ]
]

{ #category : #accessing }
ToEditorAptitude >> styler: aStyler [
	styler := aStyler.
	
	self widgetDo: [ :aWidget | aWidget editor styler: aStyler ]
]

{ #category : #accessing }
ToEditorAptitude >> text: aText [
	text := aText.
	attributesBuilder attributesTarget: text.
	
	self widgetDo: [ :aWidget | aWidget editor text: aText ]
]
