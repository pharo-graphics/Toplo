"
A ToSpaceSkinPhasesManagerTest is a test class for testing the behavior of ToSpaceSkinPhasesManager
"
Class {
	#name : #ToSpaceSkinPhasesManagerTest,
	#superclass : #ToTestCaseWithSpace,
	#category : #'Toplo-Tests-Core-SpaceFrame'
}

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testApplyAllEnqueuedStatesIn [

	| e |
	e := ToElement new.
	space root addChild: e.
	space applyAllSkinInstallers.
	e skinManager
		enqueueSkinState: (ToFocusState new focused: true)
		in: e.
	self assert: e skinManager skinStateQueue nextStates size > 0.
	space skinPhasesManager applyAllEnqueuedStatesIn: space.
	self assert: e skinManager skinStateQueue nextStates size equals: 0
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testApplyAllSkinInstallersIn [

	space skinPhasesManager applyAllSkinInstallersIn: space.
	self deny: space skinPhasesManager needSkinInstallPass 

]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testCheckSkinPhasesIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed.
	self deny: space skinPhasesManager skinPhasesInstalled.
	space checkSkinPhases.
	" now allow skin phases "
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed.
	space skinPhasesManager startSkinPhasesIn: space.
	" since skin phases are allowed and the space is opened "
	self deny: space skinPhasesManager isSkinPhasesInstallationPostponed.
	self assert: space skinPhasesManager skinPhasesInstalled
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testInitialize [

	| m |
	m := ToSpaceSkinPhasesManager new.
	self deny: m skinPhasesInstalled.
	self deny: m  isSkinPhasesInstallationPostponed.
	self assert: m needSkinInstallPass 
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testInstalledSkinInstallerPhaseIn [

	space skinPhasesManager startSkinPhasesIn: space.
	self assert: ((space skinPhasesManager installedSkinInstallerPhaseIn: space) isKindOf: ToSpaceSkinInstallerPhase).


]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testInstalledSkinStateApplicationPhaseIn [

	space skinPhasesManager startSkinPhasesIn: space.
	self assert: ((space skinPhasesManager installedSkinStateApplicationPhaseIn: space) isKindOf: ToSpaceSkinStateApplicationPhase).
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testIsSkinInstallerPhaseInstalledIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self deny: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).
	space skinPhasesManager startSkinPhasesIn: space.
	self assert: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).

]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testIsSkinPhasesInstallationPostponed [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self assert: (space skinPhasesManager isSkinPhasesInstallationPostponed).
	space skinPhasesManager startSkinPhasesIn: space.
	self deny: (space skinPhasesManager isSkinPhasesInstallationPostponed).



]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testIsSkinStateApplicationPhaseInstalledIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self deny: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).
	space skinPhasesManager startSkinPhasesIn: space.
	self assert: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).

]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testNeedSkinInstallPass [

	| e |
	e := ToElement new.
	space applyAllSkinInstallers.
	self deny: space skinPhasesManager needSkinInstallPass.
	space root addChild: e.
	self assert: space skinPhasesManager needSkinInstallPass.
	space applyAllSkinInstallers.
	" the previous skin installation has armed a skin installer for the space root "
	self assert: space skinPhasesManager needSkinInstallPass.
	space applyAllSkinInstallers.
	" all skin installers should have been applied "
	self deny: space skinPhasesManager needSkinInstallPass.
	e addStamp: #stamp.
	self assert: space skinPhasesManager needSkinInstallPass.
	space applyAllSkinInstallers.
	" all skin installers should have been applied "
	self deny: space skinPhasesManager needSkinInstallPass.

	



	
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testNeedSkinStateApplicationPass [

	| e |
	e := ToElement new.
	space applyAllSkinInstallers.
	self deny: space skinPhasesManager needSkinInstallPass.
	self assert: space skinPhasesManager needSkinStateApplicationPass.
	space applyAllEnqueuedStates.
	self deny: space skinPhasesManager needSkinStateApplicationPass.
	space root addChild: e.
	self assert: space skinPhasesManager needSkinInstallPass.
	self deny: space skinPhasesManager needSkinStateApplicationPass.
	space applyAllSkinInstallers.
	self assert: space skinPhasesManager needSkinStateApplicationPass.
	space applyAllEnqueuedStates.
	self deny: space skinPhasesManager needSkinStateApplicationPass.

	space applyAllSkinInstallers.
	self deny: space skinPhasesManager needSkinStateApplicationPass.
	self deny: space skinPhasesManager needSkinInstallPass.
	e addStamp: #stamp.
	self assert: space skinPhasesManager needSkinInstallPass.
	self deny: space skinPhasesManager needSkinStateApplicationPass.




	
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testOnInstalledIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed.
	space skinPhasesManager stopSkinPhasesIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager onInstalledIn: space.
	" since space is not opened, the skin phases installation should be postponed "
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testOnInstalledIn2 [

	space skinPhasesManager startSkinPhasesIn: space.
	" directly install (bypass the space opened/closed checking) "
	self deny: space skinPhasesManager isSkinPhasesInstallationPostponed.
	space skinPhasesManager stopSkinPhasesIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager onInstalledIn: space.
	self deny: space skinPhasesManager skinPhasesInstalled.
	" since space is not opened, the skin phases installation should be postponed "
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testOnUninstalledIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed.
	space skinPhasesManager onUninstalledIn: space.
	" here nothing is installed nor postponed"
	self deny: space skinPhasesManager isSkinPhasesInstallationPostponed.
	self deny: space skinPhasesManager skinPhasesInstalled
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testPostponeStartSkinPhasesIn [

	space checkSkinPhases.
	space skinPhasesManager onUninstalledIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager postponeStartSkinPhasesIn: space.
	self assert: space skinPhasesManager isSkinPhasesInstallationPostponed
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testSkinPhasesInstalled [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self deny: (space skinPhasesManager skinPhasesInstalled).
	space skinPhasesManager startSkinPhasesIn: space.
	self assert: (space skinPhasesManager skinPhasesInstalled).

]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testStartSkinInstallerPhaseIn [

	space checkSkinPhases.
	space skinPhasesManager onUninstalledIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager startSkinInstallerPhaseIn: space.
	self assert: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space)
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testStartSkinPhasesIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self deny: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).
	space skinPhasesManager startSkinPhasesIn: space.
	self assert: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).
	self assert: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).

]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testStartSkinStateApplicationPhaseIn [

	space checkSkinPhases.
	space skinPhasesManager onUninstalledIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager startSkinStateApplicationPhaseIn: space.
	self assert: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space)
]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testStopSkinInstallerPhaseIn [

	space checkSkinPhases.
	space skinPhasesManager onUninstalledIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager startSkinInstallerPhaseIn: space.
	self assert: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).
	space skinPhasesManager stopSkinInstallerPhaseIn: space.
	self deny: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).

]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testStopSkinPhasesIn [

	space checkSkinPhases.
	" since space is not opened, the skin phases installation should be postponed "
	self deny: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).
	space skinPhasesManager startSkinPhasesIn: space.
	self assert: space skinPhasesManager skinPhasesInstalled.
	self assert: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).
	self assert: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).
	space skinPhasesManager stopSkinPhasesIn: space.
	self deny: space skinPhasesManager skinPhasesInstalled.
	self deny: (space skinPhasesManager isSkinInstallerPhaseInstalledIn: space).
	self deny: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).



]

{ #category : #tests }
ToSpaceSkinPhasesManagerTest >> testStopSkinStateApplicationPhaseIn [

	space checkSkinPhases.
	space skinPhasesManager onUninstalledIn: space.
	" here nothing is installed nor postponed"
	space skinPhasesManager startSkinStateApplicationPhaseIn: space.
	self assert: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space).
	space skinPhasesManager stopSkinStateApplicationPhaseIn: space.
	self deny: (space skinPhasesManager isSkinStateApplicationPhaseInstalledIn: space)
]
