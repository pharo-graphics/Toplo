Class {
	#name : #ToLabelSkin,
	#superclass : #ToRawSkin,
	#instVars : [
		'foreground'
	],
	#category : #'Toplo-Widget-Album-Label'
}

{ #category : #'event handling' }
ToLabelSkin >> deselectedSkinEvent: anEvent [

	super deselectedSkinEvent: anEvent.
	anEvent selectionMode isPrimary ifFalse: [ ^ self ].
	anEvent selectionMode selectionOption isMasked ifTrue: [ ^ self ].
	anEvent selectionMode selectionOption hasEmbeddedRepresentation ifTrue: [ ^ self ].
	anEvent elementDo: [ :e |
		"e text asString trace. ' ' trace. anEvent traceCr."
		e foreground: foreground ]
]

{ #category : #'event handling' }
ToLabelSkin >> disabledSkinEvent: anEvent [

	super disabledSkinEvent: anEvent.
	anEvent elementDo: [ :e |
		"e text asString trace. ' ' trace. anEvent traceCr."
		e foreground: (e valueOfTokenNamed: #'color-text-disabled').
		e applyStyle ]
]

{ #category : #'event handling' }
ToLabelSkin >> enabledSkinEvent: anEvent [

	super enabledSkinEvent: anEvent.
	anEvent elementDo: [ :e |
		"e text asString trace. ' ' trace. anEvent traceCr."
		e foreground: foreground.
		e applyStyle ]
]

{ #category : #'event handling' }
ToLabelSkin >> installSkinEvent: anEvent [

	super installSkinEvent: anEvent.
	anEvent elementDo: [ :e |
		"e text asString trace. ' ' trace. anEvent traceCr."
		e withStamp: #foreground
					do: [ :v | foreground := v ]
					ifNone: [  foreground := e valueOfTokenNamed: #'color-text' ].
		e textAttributesBuilder
			defaultFontSize: (e valueOfTokenNamed: #'font-size');
			lineSpacing: (e valueOfTokenNamed: #'line-height');
			foreground: foreground.
		e applyStyle ]
]

{ #category : #'event handling' }
ToLabelSkin >> selectedSkinEvent: anEvent [

	super selectedSkinEvent: anEvent.
	anEvent selectionMode isPrimary ifFalse: [ ^ self ].
	anEvent selectionMode selectionOption isMasked ifTrue: [ ^ self ].
	anEvent selectionMode selectionOption hasEmbeddedRepresentation ifTrue: [ ^ self ].
	anEvent elementDo: [ :e |
		| f |
		"e text asString trace. ' ' trace. anEvent traceCr."
		anEvent focused
			ifTrue: [
				e
					withStamp: #'label-selected-focused-foreground'
					do: [ :v | f := v ]
					ifNone: [
					f := e valueOfTokenNamed: #'label-selected-focused' ] ]
			ifFalse: [
				e
					withStamp: #'label-selected-unfocused-foreground'
					do: [ :v | f := v ]
					ifNone: [
					f := e valueOfTokenNamed: #'label-selected-unfocused' ] ].
		e foreground: f.
		e applyStyle ]
]
